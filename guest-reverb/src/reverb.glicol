~wet: ~input >> onepole #bandwidth
>> delayms 50 >> apfgain 4.771 0.75
>> apfgain 3.595 0.75
>> apfgain 12.72 0.625
>> apfgain 9.307 0.625
>> add ~back
>> apfgain ~modu 0.7;

~modu: sin 0.1 >> mul 5.5 >> add 29.5;

~aa: ~wet >> delayn 394.0;
~ab: ~aa >> delayn 2800.0;
~ac: ~ab >> delayn 1204.0;

~ba: ~ac >> delayn 2000.0 >> onepole #damping
>> apfgain 7.596 0.5;
~bb: ~ba >> apfgain 35.78 0.5;
~bc: ~bb >> apfgain ~modu 0.5;

~ca: ~bc >> delayn 179.0;
~cb: ~ca >> delayn 2679.0;
~cc: ~cb >> delayn 3500.0 >> mul #decay;

~da: ~cc >> apfgain 30.1 0.7 >> delayn 522.0;
~db: ~da >> delayn 2400.0;
~dc: ~db >> delayn 2400.0;

~ea: ~dc >> onepole #damping >> apfgain 6.2 0.7;
~eb: ~ea >> apfgain 34.92 0.7;

~fa: ~eb >> apfgain 20.4 0.7 >> delayn 1578.0;
~fb: ~fa >> delayn 2378.0;

~back: ~fb >> delayn 2500.0 >> mul #decay;

~subtract_left: ~bb >> add ~db >> add ~ea >> add ~fa 
>> mul -1.0;
~left: ~aa >> add ~ab >> add ~cb >> add ~subtract_left
>> mul #wetmix >> add ~drym;

~sub_right: ~eb >> add ~ab >> add ~ba >> add ~ca 
>> mul -1.0;
~right: ~da >> add ~db >> add ~fb >> add ~sub_right
>> mul #wetmix >> add ~drym;

~drym: ~input >> mul #drymix

output: balance ~left ~right;